# tool: scribe-data
# All Persian (Q9168) verbs and the given perfect tense forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?firstPersonSingPerfect
  ?secondPersonSingPerfect
  ?thirdPersonSingPerfect
  ?firstPersonPluralPerfect
  ?secondPersonPluralPerfect
  ?thirdPersonPluralPerfect

WHERE {
  ?lexeme dct:language wd:Q9168;
          wikibase:lexicalCategory wd:Q24905.

  # MARK: Infinitive
  ?lexeme ontolex:lexicalForm ?infinitiveForm .
  ?infinitiveForm ontolex:representation ?infinitive ;
    wikibase:grammaticalFeature wd:Q179230 .

  # MARK: Perfect Tense Forms
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonSingPerfectForm.
    ?firstPersonSingPerfectForm ontolex:representation ?firstPersonSingPerfect;
      wikibase:grammaticalFeature wd:Q625420 , wd:Q21714344 , wd:Q110786.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonSingPerfectForm.
    ?secondPersonSingPerfectForm ontolex:representation ?secondPersonSingPerfect;
      wikibase:grammaticalFeature wd:Q625420 , wd:Q51929049 , wd:Q110786.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonSingPerfectForm.
    ?thirdPersonSingPerfectForm ontolex:representation ?thirdPersonSingPerfect;
      wikibase:grammaticalFeature wd:Q625420 , wd:Q51929074 , wd:Q110786.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonPluralPerfectForm.
    ?firstPersonPluralPerfectForm ontolex:representation ?firstPersonPluralPerfect;
      wikibase:grammaticalFeature wd:Q625420 , wd:Q21714344 , wd:Q146786.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonPluralPerfectForm.
    ?secondPersonPluralPerfectForm ontolex:representation ?secondPersonPluralPerfect;
      wikibase:grammaticalFeature wd:Q625420 , wd:Q51929049 , wd:Q146786.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonPluralPerfectForm.
    ?thirdPersonPluralPerfectForm ontolex:representation ?thirdPersonPluralPerfect;
      wikibase:grammaticalFeature wd:Q625420 , wd:Q51929074 , wd:Q146786.
  }
}
