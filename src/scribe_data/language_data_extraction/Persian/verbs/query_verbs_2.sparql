# tool: scribe-data
# All Persian (Q9168) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?firstPersonSingPresent
  ?secondPersonSingPresent
  ?thirdPersonSingPresent
  ?firstPersonPluralPresent
  ?secondPersonPluralPresent
  ?thirdPersonPluralPresent

WHERE {
  ?lexeme dct:language wd:Q9168;
          wikibase:lexicalCategory wd:Q24905.

  # MARK: Infinitive
  ?lexeme ontolex:lexicalForm ?infinitiveForm .
  ?infinitiveForm ontolex:representation ?infinitive ;
    wikibase:grammaticalFeature wd:Q179230 .

  # MARK: Present Tense Forms
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonSingPresentForm.
    ?firstPersonSingPresentForm ontolex:representation ?firstPersonSingPresent;
      wikibase:grammaticalFeature wd:Q21714344 , wd:Q110786 , wd:Q192613.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonSingPresentForm.
    ?secondPersonSingPresentForm ontolex:representation ?secondPersonSingPresent;
      wikibase:grammaticalFeature wd:Q51929049 , wd:Q110786 , wd:Q192613.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonSingPresentForm.
    ?thirdPersonSingPresentForm ontolex:representation ?thirdPersonSingPresent;
      wikibase:grammaticalFeature wd:Q51929074 , wd:Q110786 , wd:Q192613.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonPluralPresentForm.
    ?firstPersonPluralPresentForm ontolex:representation ?firstPersonPluralPresent;
      wikibase:grammaticalFeature wd:Q21714344 , wd:Q146786 , wd:Q192613.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonPluralPresentForm.
    ?secondPersonPluralPresentForm ontolex:representation ?secondPersonPluralPresent;
      wikibase:grammaticalFeature wd:Q51929049 , wd:Q146786 , wd:Q192613.
  }
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonPluralPresentForm.
    ?thirdPersonPluralPresentForm ontolex:representation ?thirdPersonPluralPresent;
      wikibase:grammaticalFeature wd:Q51929074 , wd:Q146786 , wd:Q192613.
  }
}
